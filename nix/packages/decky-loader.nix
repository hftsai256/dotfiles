{ lib
, fetchFromGitHub
, nodejs
, pnpm_9
, fetchPnpmDeps
, pnpmConfigHook
, python3
, coreutils
, psmisc
}:
python3.pkgs.buildPythonPackage rec {
  pname = "decky-loader";
  version = "3.2.1";

  src = fetchFromGitHub {
    owner = "SteamDeckHomebrew";
    repo = "decky-loader";
    rev = "v${version}";
    hash = "sha256-E4OdUAu7H6k9vKxUmD0eQdLNG4c1HBw7fVjgqfgpNNE=";
  };

  pnpmDeps = fetchPnpmDeps {
    inherit pname version src;
    sourceRoot = "${src.name}/frontend";
    pnpm = pnpm_9;
    fetcherVersion = 3;
    hash = "sha256-Kw1I+r1zsJJ+0bi0RyfG7LYyXnRW/+nKod6KKr/xrnk=";
  };

  pyproject = true;
  pnpmRoot = "frontend";

  nativeBuildInputs = [ nodejs pnpm_9 pnpmConfigHook ];

  preBuild = ''
    cd frontend
    pnpm build
    cd ../backend
  '';

  build-system = with python3.pkgs; [ poetry-core poetry-dynamic-versioning ];

  dependencies = with python3.pkgs; [
    aiohttp aiohttp-cors aiohttp-jinja2
    certifi multidict packaging setproctitle watchdog
  ];

  makeWrapperArgs = [
    "--prefix PATH : ${lib.makeBinPath [ coreutils psmisc ]}"
  ];

  pythonRelaxDeps = [ "aiohttp-cors" "packaging" "watchdog" ];

  passthru.python = python3;

  meta = with lib; {
    description = "A plugin loader for the Steam Deck";
    homepage = "https://github.com/SteamDeckHomebrew/decky-loader";
    platforms = platforms.linux;
    license = licenses.gpl2Only;
  };
}
